buildscript {
    ext {
        grailsVersion = project.grailsVersion
    }
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
    }
}

plugins {
    id "io.spring.dependency-management" version "0.5.1.RELEASE"
    id "com.jfrog.bintray" version "1.1"
}

version "2.0-SNAPSHOT"
group "org.grails.plugins"

apply plugin: 'maven-publish'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: "spring-boot"
apply plugin: "org.grails.grails-plugin"
apply plugin: "org.grails.grails-gsp"
// Used for publishing to central repository, remove if not needed
apply from: 'https://raw.githubusercontent.com/grails/grails-profile-repository/master/profiles/plugin/templates/grailsCentralPublishing.gradle'
apply from: 'https://raw.githubusercontent.com/grails/grails-profile-repository/master/profiles/plugin/templates/bintrayPublishing.gradle'

ext {
    grailsVersion = project.grailsVersion
    gradleWrapperVersion = project.gradleWrapperVersion
}

sourceCompatibility = 1.7
targetCompatibility = 1.7


repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://repo.grails.org/grails/core" }
    //com.jhlabs:imaging (a dependency of 'com.octo.captcha:jcaptcha:1.0') is not in Maven Central. Reported issue at https://issues.sonatype.org/browse/MVNCENTRAL-463
    maven { url 'http://maven.jahia.org/maven2/' }

    //org.mobicents.external.freetts:freetts:1.2.2 is not in Maven Central.
    maven { url 'http://maven.it.su.se/nexus/content/groups/public/' }
}

dependencyManagement {
    imports {
        mavenBom "org.grails:grails-bom:$grailsVersion"
    }
    applyMavenExclusions false
}

dependencies {
    provided 'org.springframework.boot:spring-boot-starter-logging'
    provided "org.springframework.boot:spring-boot-starter-actuator"
    provided "org.springframework.boot:spring-boot-autoconfigure"
    provided "org.springframework.boot:spring-boot-starter-tomcat"

    provided "org.grails:grails-web-boot"
    provided "org.grails:grails-dependencies"
    provided 'javax.servlet:javax.servlet-api:3.1.0'

    compile('com.octo.captcha:jcaptcha:1.0') {
        exclude group: 'javax.servlet', module: 'servlet-api'
        exclude group: 'com.jhlabs', module: 'imaging'
    }
    compile('com.jhlabs:filters:2.0.235-1')
    compile('com.octo.captcha:jcaptcha-extension-sound-freetts:1.0') {
        exclude group: 'com.octo.captcha', module: 'jcaptcha-common-test'
        exclude group: 'com.sun.speech.freetts', module: 'freetts'
        exclude group: 'com.octo.captcha', module: 'jcaptcha'
        exclude group: 'com.jhlabs', module: 'imaging'
        exclude group: 'com.sun.speech.freetts', module: 'freetts'
    }
    compile 'org.mobicents.external.freetts:freetts:1.2.2'
    compile 'org.mobicents.external.freetts:cmu_time_awb:1.2.2'
    compile 'org.mobicents.external.freetts:cmu_us_kal:1.2.2'
    compile 'org.mobicents.external.freetts:en_us:1.2.2'
    compile 'org.mobicents.external.freetts:cmulex:1.2.2'
    compile 'org.mobicents.external.freetts:cmutimelex:1.2.2'


    testCompile "org.grails:grails-plugin-testing"

    console "org.grails:grails-console"
}

task wrapper(type: Wrapper) {
    gradleVersion = gradleWrapperVersion
}
